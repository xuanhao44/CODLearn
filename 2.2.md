# RISC-V 冒泡排序

## 冒泡排序

```c
void sort (long long int v[], size_t n) {
	size_t i, j;
	for (i = 0; i < n; i += 1) {
		for (j = i – 1;j >= 0 && v[j] > v[j + 1];j -= 1) {
			swap(v,j);
		}
	}
}
```

```c
void swap(long long int v[], long long int k) { 
	long long int temp; 
	temp = v[k]; 
	v[k] = v[k + 1]; 
	v[k + 1] = temp; 
} 
```

- 给定的算法比较简陋，没有用到标志，而是傻乎乎的从头比较到尾。
- 做的是升序排列。
- swap 函数交换的是数组 v 的第 k 位和第 k+1 位，所以参数是 数组 v[] 和 k，看起来比较奇怪。

## swap 过程

v[] 保存在 x10，k 保存在 x11，temp 保存在 x5。

```c
void swap(long long int v[], long long int k) { 
	long long int temp; 
	temp = v[k]; 
	v[k] = v[k + 1]; 
	v[k + 1] = temp; 
} 
```

```asm
slli x6,x11,3  # x6 = k * 8 
add  x6,x10,x6 # x6 = v + (k * 8) 
ld   x5,0(x6)  # x5(temp) = v[k] 
ld   x7,8(x6)  # x7 = v[k + 1]
sd   x7,0(x6)  # v[k] = x7 
sd   x5,8(x6)  # v[k + 1] = x5(temp)
jalr x0,0(x1)  # 返回调用函数
```

## sort 过程

v[] 保存在 x10，n 保存在 x11，i 保存在 x19，j 保存在 x20。

```c
void sort (long long int v[], size_t n) {
	size_t i, j;
	for (i = 0; i < n; i += 1) {
		for (j = i – 1;j >= 0 && v[j] > v[j + 1];j -= 1) {
			swap(v,j);
		}
	}
}
```

```asm
# 保存寄存器（入栈）
sort: 
	addi sp ,sp,-40    # 在栈中留出 5 个寄存器（双字）的空间
	sd   x1 ,32(sp)    # 保存 x1  的值（入栈）
	sd   x22,24(sp)    # 保存 x22 的值（入栈）
	sd   x21,16(sp)    # 保存 x21 的值（入栈）
	sd   x20,8(sp)     # 保存 x20 的值（入栈）
	sd   x19,0(sp)     # 保存 x19 的值（入栈）
# 移动参数
mv x21,x10           # 复制 x10 中的值到 x21
mv x22,x11           # 复制 x11 中的值到 x22
# 外循环
li x19,0             # i = 0
for1tst:
	bge  x19,x22,exit1 # 如果 x19 ≥ x22(i ≥ n)，跳转到 exit1
# 内循环
	addi x20,x19,-1    # j = i - 1
for2tst:
	blt  x20,x0,exit2  # 如果 x20 < 0 (j < 0)，跳转到 exit2
	slli x5 ,x20,3     # x5 = j * 8
	add  x5 ,x21,x5    # x5 = v + (j * 8)
	ld   x6 ,0(x5)     # x6 = v[j]
	ld   x7 ,8(x5)     # x7 = v[j + 1]
	ble  x6 ,x7,exit2  # 如果 x6 ≤ x7，跳转到 exit2
# 参数传递和调用
	mv   x10,x21       # swap 的第一个参数是 v
	mv   x11,x20       # swap 的第二个参数是 j
	jal  x1 ,swap      # 调用 swap
# 内循环增值
	addi x20,x20,-1    # j -= 1
	j    for2tst       # 跳转到内层循环 for2tst
# 外循环增值
exit2: 
	addi x19,x19,1     # i += 1
	j    for1tst       # 跳转到外层循环的判断语句
# 恢复寄存器
exit1:
	ld   x19,0(sp)     # 恢复 x19（出栈）
	ld   x20,8(sp)     # 恢复 x20（出栈）
	ld   x21,16(sp)    # 恢复 x21（出栈）
	ld   x22,24(sp)    # 恢复 x22（出栈）
	ld   x1 ,32(sp)    # 恢复 x1 （出栈）
	addi sp ,sp,40     # 恢复栈指针
# 过程返回
jalr x0,0(x1)        # 返回调用线程
```

