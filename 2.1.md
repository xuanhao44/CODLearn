# jump!

## 条件分支

- `bge reg1,reg2,label #branch if greater than or equal`

  - 如果 reg1 >= reg2, 程序跳转到 label 处继续执行

  - 条件为真则转到标签（立即数）所指的语句执行，否则按序执行。

- 例子 1：`if (a > b) a += 1;` 化为 RISC-V 汇编。假设：a 保存在 x22，b 保存在 x23。

  ```asm
  bge x23, x22, Exit # 当 b >= a 时跳转
  addi x22, x22, 1 # a = a + 1
  Exit:...
  ```

  假设 a = 1，b = 2，那么跳转到 Exit，顺序执行 Exit 之后的指令；

  假设 a = 2，b = 1，那么不跳转，顺序执行。

## 无条件跳转

- `jal rd,Label # jump and link`

  - **将下一条指令的地址 PC + 4 保存在寄存器 rd**（一般用 x1）

  - 跳转到目标地址 Label（立即数偏移量，有规定的转换规则）
  - 伪指令 `j Label` ，原型是 `jal, x0, Label`，也就是不保存下一条指令地址，不返回

- `jalr rd, offset(rs1)  # jump and link register`

  - 类似 jal，但跳转到 rs1 + offset 地址处的指令
  - 把 PC + 4 存到 rd 中，如果 rd 用 x0，相当于只跳转不返回

- 例子 2：f → x10，g → x11，h → x12，i → x13，j → x14

  ```c
  if (i == j)
  	f = g + h;
  else
  	f = g – h;
  end
  ...
  ```

  ```asm
  bne x13, x14, Else
  add x10, x11, x12
  j Exit
  Else: sub x10, x11, x12
  Exit:
  ```

  `j Exit` 的用途是避免顺序执行到 Else 语句。

## 循环结构

### 例子 3：for 结构

数组基地址保存在 x8。

```c
long long int A[20];
long long int sum = 0;
for (long long int i = 0; i < 20; i++)
	sum += A[i];
```

```asm
add  x9 , x8, x0 # x9 = &A[0]
add  x10, x0, x0 # sum = 0
add  x11, x0, x0 # i = 0
addi x13, x0, 20 # x13 = 20
Loop:
	bge  x11, x13, Done
	ld   x12, 0(x9)  # x12 = A[i]
	add  x10, x10, x12
	addi x9 , x9, 8  # &A[i+1]
	addi x11, x11, 1 # i++
	j Loop
Done:
```

储存数组基地址的寄存器一般不直接拿出来用，而是赋给其他的寄存器使用地址，如 `add x9,x8,x0`。

### 例子 4：while 结构

i 存储在 x22 中，k 存储在 x24 中，数组 save 的地址保存在 x25 中。数组 save 为 **long long int**。

```c
while (save[i] == k)
  i += 1;
```

```asm
Loop:
	slli x10, x22, 3     # i << 3 = 8i
	add  x10, x10, x25   # x10 = &save[i]
	ld   x9 , 0(x10)     # x9 = save[i]
	bne  x9 , x24, Exit  # save[i] = k?
	addi x22, x22, 1     # i = i + 1
	beq  x0 , x0,  Loop  # j Loop
Exit: ...
```

`beq x0,x0,Loop` 和 `j Loop` 的作用是相同的。

## 函数调用

### 函数调用指令

- 过程调用：跳转和链接 `jal ra, Label`
  - 将下一条指令的地址保存在 x1 中
  - 跳转到 Label（Label对应指令的目标地址）
- 过程返回：寄存器跳转和链接 `jalr x0, 0(ra)`
  - 类似 jal，但跳转到 0 + x1中保存的地址
  - 把 x0 用作目的寄存器（实际 x0 不会被改变）
  - 同样可用于跳转到计算出的位置
  - 伪指令：`jr ra`，原型是 `jalr x0, 0(ra)`

### 例子 5：简单的 a + b 函数

参数 a 存入寄存器 s0，参数 b 存入寄存器 s1。

调用 sum 函数必须能够以某种方式返回。

```c
int sum(int a, int b) {
	return (a + b);
}
```

1000，1004...代指指令地址

方法 1：

```asm
1000 mv a0, s0
1004 mv a1, s1
1008 addi ra, x0, 1016 # ra = 1016
1012 j sum # jump to sum
1016 … # 下个指令
…
2000 sum: add a0, a0, a1 # a0 = a0 + a1 
2004 jr ra # jump register; jalr x0, 0(ra)
```

方法 2：

```asm
1000 mv a0, s0
1004 mv a1, s1
1008 jal ra, sum # ra = 1012, jump to sum
1012 # 下个指令
…
2000 sum: add a0, a0, a1
2004 jr ra # jump register
```

ra 总是要存储函数执行完后的下一条指令的地址的。区别就是 addi 是手动存储的，而 jal 本来就会在 rd 的位置存储下一条指令的地址。

### 例子 6：字符串复制（栈相关）

- 以 null 字符结束的字符串。
- 用 x10 保存 x[ ] 的基址，x11 保存 y[ ] 的基址，x19 保存 i。
- 把 y[] copy 到 x[]。

```c
void strcpy (char x[], char y[]) { 
	int i; 
	i = 0; 
	while ((x[i] = y[i])!='\0') 
		i += 1;
}
```

- char：8 位，一个字符用一个字节表示
- x19 是保存寄存器，可能含有主程序需要用到的值，需入栈保留

```asm
strcpy:
	addi sp ,sp,-8   # 调整栈，留出 1 个双字的空间
	sd   x19,0(sp)   # x19 入栈 保护调用函数的数据
	add  x19,x0,x0   # i = 0
L1:
	add  x5 ,x19,x11 # x5 = y[i] 的地址
	lbu  x6 ,0(x5)   # x6 = y[i]
	add  x7 ,x19,x10 # x7 = x[i] 的地址
	sb   x6 ,0(x7)   # x[i] = y[i]
	beq  x6 ,x0,L2   # 若 y[i] == 0，则退出
	addi x19,x19,1   # i = i + 1
	jal  x0 ,L1      # 下一次 loop 迭代
L2:
	ld   x19,0(sp)   # 恢复 x19 原值
	addi sp ,sp,8    # 从栈中弹出 1 个双字
	jalr x0 ,0(x1)   # 返回主函数
```

 汇编：[strcpy.asm](_srcs\strcpy.asm)，注意为 RV-64I。

### 例子 7：函数嵌套（栈相关）

- x → a0，y → a1
- mult 函数，假设
  - mult 的入口参数在 a0 和 a1 两个寄存器中。
  - mult 的计算结果，由寄存器 a0 返回。

```c
int numSquare (int x, int y) {
	return mult (x, x) + y;
}
```

```asm
numSquare:
	addi sp, sp,-8 # push1:space on stack
	sw   ra, 4(sp) # push2:save return addr
	sw   al, 0(sp) # push3:save y
	mv   al, a0    # al = a0 (a0 = x)
	jal  ra, mult  # call mult
	lw   al, 0(sp) # restore y
	add  a0, a0,a1 # mult() + y
	lw   ra, 4(sp) # pop1:get ret addr
	addi sp, sp,8  # pop2:restore stack
	jr   ra        # return to caller function
mult:
	…
```

补充与解释：

1. numSquare 的计算结果也是由 a0 返回的。
2. mult 里应有 jr ra 这样的指令。
3. ra 是返回地址的通用寄存器，这里有两个函数都需要使用 ra，所以 ra 在使用前存入了栈中。
   - mult 计算完成后返回到 numSquare 的某处
   - numSquare 计算完成后返回到主函数某处
4. 这里能用的寄存器太少，以至于需要把 a0 和 a1 玩出花样来：先把 a1(y) 的值存起来，又把 a0(x) 的值赋给 a1(y)，a0(x) 自己参与 mult 函数计算之后变为 mult()，那么这时再把 a1(y) 取出来继续完成 numSquare 的计算，最后把计算结果由 a0 返回...总之很绕，老师讲的不是很好。

### 例子 8：用栈进行递归——阶乘

参数 n 在 a0 中；结果在 a0 中。

```c
long long int fact (long long int n) { 
	if (n < 1) return 1; 
		else return n * fact(n - 1);
}
```

```asm
fact:
	addi sp,sp,-16 # 留出 2 个双字的栈空间
	sd   ra,8(sp)  # 返回地址保存到栈中
	sd   a0,0(sp)  # 参数 n 保存到栈中
	addi t0,a0,-1
	bge  t0,x0,L1  # 若参数 n - 1 >= 0，则跳转到 L1
	addi a0,x0,1   # 若 n - 1 < 0，返回值 1 并存入 a0
	addi sp,sp,16  # 释放 2 个双字的栈空间，不用恢复
	jalr x0,0(ra)  # 返回 fact 的调用者
L1:
	addi a0,a0,-1  # n = n - 1
	jal  ra,fact   # 调用 fact(n - 1)，重复直到 fact(0)
Calc:
	addi t1,a0,0   # 从 fact(0) 开始，结果存到 t1
	ld   a0,0(sp)  # 恢复调用者的参数
	ld   ra,8(sp)  # 恢复调用者的返回地址
	addi sp,sp,16  # 释放 2 个双字的栈空间
	mul  a0,a0,t1  # m * fact(m - 1)（m 从 1 开始）
	jalr x0,0(ra)  # 返回 Calc
```

**详细递归过程见栈动画 PPT。**

补充与解释：

1. Calc 是实际上的 fact 函数，不要和汇编代码的 fact 块混淆了。
2. $X_{1,1}$ 是 Calc 函数返回主函数的 ra 地址。
3. 概括的说，fact 完成了入栈的工作；L1 完成了重复调用下一级 fact(n-1) 的工作；Calc 完成了出栈的工作。
4. 进一步说，fact 还判断了是否需要继续向下调用；L1 还改变了每一次的 ra，使其指向 Calc；Calc 完成了每一次的乘法。
5. 这个程序仍然是不完全的。

### 例子 9：用栈进行递归——斐波那契数列

C 语言：

```c
int FIB(int n) {
	if (n == 1 || n == 2) return 1;
	return FIB(n-1) + FIB(n-2);
}
```

C 语言改写：

```c
int FIB(int arg) {
  
  int temp_1, temp2;
  
  if (arg == 1) {
    return arg;
  }
  
  if (arg == 2) {
    arg == 1;
    return arg;
  }
  
  int temp_arg = arg;
  
  arg = temp_arg - 1;
  arg = FIB(arg);
  temp_1 = arg;
  
  arg = temp_arg - 2;
  arg = FIB(arg);
  temp_2 = arg;
  
  arg = temp_1 + tmep_2;
  return arg;
}
```

参数为 a0，返回为 a0

```asm
MAIN:
	lui  t0,0x00002 # 存入地址
	lw   a0,0x0(t0) # 从主存读数据

	jal  ra, FIB
	addi s3,a0,0    # 数据结果放入 s3
	jal  x0, FINAL

FIB:
	addi sp,sp,-16 # 留出 4 个字的栈空间
	sw   ra,4(sp)  # 返回地址保存到栈中
	sw   t1,8(sp)  # temp_1
	sw   t2,12(sp) # temp_2
	sw   a0,16(sp) # arg
	
	addi t3,a0,-1
	beq  t3,x0,OUT # 若参数 n = 1，则跳转到 OUT
	j L1
	addi t3,t3,-1
	beq  t3,x0,OUT # 若参数 n = 2，则跳转到 OUT
	
OUT:
	addi a0,x0,1   # 返回值 1 并存入 a0
	addi sp,sp,16  # 释放 4 个字的栈空间，不用恢复
	jalr x0,0(ra)  # 回到主程序

  addi t0,a0,0   # temp_arg = arg
L1:
	addi a0,t0,-1  # arg = temp_arg - 1
	jal  ra,FIB    # arg = FIB(arg)
	addi t1,a0,0   # temp_1 = arg
L2:
	addi a0,t0,-2  # arg = temp_arg - 2
	jal  ra,FIB    # arg = FIB(arg)
	addi t2,a0,0   # temp_2 = arg
CALC:	
	addi a0,t1,0
	add  a0,a0,t2  # arg = temp_1 + temp_2;
	
	lw   t0,16(sp) # temp_arg
	lw   t2,12(sp) # temp_2
	lw   t1,8(sp)  # temp_1
	lw   ra,4(sp)  # 取回 ra
	addi sp,sp,16  # 释放 4 个字的栈空间
	jalr x0,0(ra)  # 回到主程序
FINAL:
```

